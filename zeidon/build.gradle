description ="""
Domain logic for UTC and the Zeidon objects.

Project name: ${project.name}
"""

configurations {
    repl
}

// In this section you declare the dependencies for your production and test code
dependencies {
    compile( 'com.quinsoft.zeidon:zeidon-scala:1.4.1-SNAPSHOT') {
        // Exclude some projects to keep the final .war size down.
        exclude module: 'scala-reflect'
        exclude module: 'httpclient'
        exclude module: 'commons-validator'
    }

    compile( 'org.scala-lang:scala-library:2.11.8' ) {
	exclude module: 'scala-xml_2.11'
    }
    
    compile 'org.xerial:sqlite-jdbc:3.14.2'
    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.+'

    repl 'org.scala-lang:scala-compiler:2.11.8'
    repl 'jline:jline:2.13'
}

task repl(type:JavaExec) {

  def initFile = ''

  File.createTempFile("temp",".tmp").with {
    deleteOnExit()
    write '''
import com.quinsoft.zeidon.standardoe.JavaObjectEngine
import com.quinsoft.zeidon.scala._

val oe = JavaObjectEngine.getInstance()
val task = oe.createTask("UTC")
    
'''
    initFile = absolutePath
  }

  main = "scala.tools.nsc.MainGenericRunner"
  classpath( sourceSets.main.runtimeClasspath, '../conf', configurations.repl )
  standardInput System.in
  args( '-i', initFile, '-usejavacp' )
  jvmArgs( '-DSQLITE_ROOT=../sqlite', '-Djline.inputrc=~/.jline.inputrc' )
}